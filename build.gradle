plugins {
    alias(libs.plugins.android.app) apply(false)
    alias(libs.plugins.android.lib) apply(false)
    alias(libs.plugins.kotlin.multiplatform) apply(false)
    alias(libs.plugins.kotlin.jvm) apply(false)
    alias(libs.plugins.kotlin.android) apply(false)
    alias(libs.plugins.kotlin.kapt) apply(false)
    alias(libs.plugins.kotlin.dokka) apply(false)
    alias(libs.plugins.hilt) apply(false)
    alias(libs.plugins.nexus.publish)
    alias(libs.plugins.git)
}

apply from: 'gradle/versioning.gradle'
apply from: 'gradle/maven-publish-config.gradle'

allprojects {
    ext {
        appId = 'com.vistajet.vjapp'
        versionCode = buildVersionCode()
        versionName = buildVersionName()
        androidBuildToolsVersion = '32.0.0'
        androidMinSdkVersion = 24
        androidTargetSdkVersion = 32
        androidCompileSdkVersion = 32

        developerId = 'motorro'
        developerName = 'Nikolai Kotchetkov'
        developerEmail = 'motorro@gmail.com'

        projectScm = 'https://github.com/motorro/CommonStateMachine.git'
        projectUrl = 'https://github.com/motorro/CommonStateMachine'
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            freeCompilerArgs += [
                    "-opt-in=kotlin.RequiresOptIn",
                    "-Xinline-classes"
            ]
        }
    }

    tasks.withType(Test) {
        forkEvery = 100
        testLogging {
            // set options for log level LIFECYCLE
            events "skipped", "failed"
            showExceptions true
            exceptionFormat "full"
            showCauses true
            showStackTraces true

            // set options for log level DEBUG and INFO
            debug {
                events "started", "passed", "skipped", "failed", "standardOut", "standardError"
                exceptionFormat "full"
            }
            info.events = debug.events
            info.exceptionFormat = debug.exceptionFormat

            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                    def startItem = '  ', endItem = '  '
                    def repeatLength = startItem.length() + output.length() + endItem.length()
                    println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
                }
            }
        }
    }

}

task runStateMachineTests(type: Task, dependsOn: [':commonstatemachine:allTests'], group: 'verification') {
    description 'Run unit tests for the common state machine layer.'
}

task runCoreUnitTests(type: Task, dependsOn: [':core:testDebugUnitTest'], group: 'verification') {
    description 'Run unit tests for the core layer.'
}

task runLoginUnitTests(type: Task, dependsOn: [':login:testDebugUnitTest'], group: 'verification') {
    description 'Run unit tests for the login module.'
}

task runAppUnitTests(type: Task, dependsOn: [':app:testDebugUnitTest'], group: 'verification') {
    description 'Run unit tests for the app layer.'
}


task displayVersion(type: Task, group: 'documentation') {
    description 'Display application version name'
    doLast {
        println("Application version: ${versionName}")
    }
}

task runUnitTests(
        type: Task,
        dependsOn: [
                'runStateMachineTests',
                'runCoreUnitTests',
                'runLoginUnitTests',
                'runAppUnitTests'
        ],
        group: 'verification'
) {
    description 'Run unit tests for all modules.'
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username = ossrhUsername
            password = ossrhPassword
        }
    }
}

